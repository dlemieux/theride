# World Creation Script

# NOTES
# To run this script, use @batchcommand theride
# It will automaticaly look inside the world folder for the file, and it assumes you will have a .ev extension.

# Execution Permissions: The user will need to be a superuser so they can make objects, and telport

# The script will create the game off of the current room.
# It does not touch the current room other than to add a single exit to it.

@dig Entrance;ride-room-01 = Enter The Ride;ride-entrance-01,Exit The Ride;ride-exit-01
#
@desc ride-entrance-01 = The first entrance to The Ride!
#
ride-entrance-01
#
@desc Entrance =
Excitement builds in your heart as you look up at the massive gate to Mythari Park. The wrought iron bars stretch up so high they almost touch the clouds. Sculptures of mystical beasts are interwoven into the gate as well. It would take hours to identify them all. 

Looking miniscule by comparison, a park worker stands directly in front of the gate and stares at you. To the right, another park worker waves at you gleefully from a small booth.
#
@desc ride-exit-01 = The final exit to The Ride.

# Add a bench in the entrance
@create/drop bench
#
@lock bench = get:false()
#
@desc bench = A sturdy bench full of people waiting for their friends to arrive.
#
@set bench/get_err_msg = It'd be rude to pick it up while others are still sitting.
# Set display order
@set bench/display_order = 3

# Add a heavy rock to the entrance
@create/drop rock : objects.Heavy
#
@desc rock = There's a giant theme park ahead of you, and you're here looking at a rock.
#
@set rock/consider_msg = It doesn't look good.
# Set display order
@set rock/display_order = 4

# Create the Park Pass Sales Clerk in the Entrance
@create/drop Clerk : objects.PassSalesClerk
#
@desc Clerk = This clerk is responsible for selling park passes. You should try to [|gbuy a pass|n] from them.
#
@set Clerk/talk_to_msg = Hello! Welcome to the park! If you would like to [|gbuy a pass|n], then you just have to ask!
#
@set Clerk/consider_msg = You would be evenly matched.
# Set display order
@set Clerk/display_order = 2
# Get error message
@set Clerk/get_err_msg = No touching!

# Create the gatekeeper
create/drop Gatekeeper : objects.ParkAttendant
#
@desc Gatekeeper = The gatekeeper is guarding a much smaller gate that's cut into the larger one. It takes a lot less time to open one that's human-sized! Their sole job is to keep out people who haven't bought a pass.
#
@set Gatekeeper/talk_to_msg = Hello! If you have already purchased your |mpass|n from the Clerk, then enter the [|gwalkway|n] to get into the park!
# Set display order
@set Gatekeeper/display_order = 1

# Create Walkway Room
@dig Moving Walkway;ride-room-02 : room_walkway.WalkwayRoom
# Create one way exit to the walkway room
@open walkway : exits.ParkEntranceExit = ride-room-02
#
@desc walkway = A moving walkway that leads into the interior of the park!
#
@tel ride-room-02
#
@desc Moving Walkway =
You find yourself standing on a moving walkway in a tunnel-like room, slowly ascending towards a brightly lit doorway. The air faintly smells like popcorn.

Lining the walls are many television screens flashing different segments on loop. You can look at each tv as you drift by. When you're ready to move on, you can head to the courtyard.

# Create a special tv object
@create/drop tv : room_walkway.WalkwayTv
# Set the get fail message
@set tv/get_err_msg = That is too high up to reach.

# Create the courtyard room
@dig Courtyard;ride-room-03
# Make a one way exit to the courtyard room
@open courtyard;c = ride-room-03
# Set the description on the exit
@desc courtyard = Proceed to the interior courtyard of the park!
# Switch rooms
courtyard
# Set Courtyard description
@desc Courtyard =
The central courtyard connects to all other areas of Mythari Park. A circle of paved bricks is surrounded by trees. Orchestral music is pumped out through hidden speakers. Wooden handmade signs label the dirt paths which extend outward from where you stand.

Despite the promotional material, most of the park is still under construction. It's still very popular, however, due to the |rChimera|n ride!

# Create an exit out of the park
@open exit park = ride-room-01
#
@desc exit park = Leave the park.
# Set display order
@set exit park/display_order  = 5


# Create Construction Zone
@dig Construction Zone = construction zone;zone,courtyard;c
#
@desc construction zone = This leads to an area closed off for construction. Maybe they are building a new attraction?
# Set display order
@set construction zone/display_order  = 3
#
construction zone
#
@desc Construction Zone =
You are standing in a dusty pathway that looks like it is currently being worked on.
Tools and supplies are strewn about, and there is a fence blocking off people from going any farther.
A nearby sign is advertising the attraction that will be built here soon.
#
@desc courtyard = Return to the courtyard.

# Create sign about the new ride under construction
@create/drop sign
#
@lock sign = get:false()
#
@desc sign = The sign reads: "Coming soon! The Temple of the Phoenix!"
# Set display order
@set sign/display_order = 1
# Get error message
@set sign/get_err_msg = The sign is fastened down to the ground.

## Create fence with hole to look through
@create/drop fence : objects.ConstructionFence
#
@lock fence = get:false()
#
@desc fence = The fence looks sturdy, although there is a tiny hole that you might be able to look through.
# Set display order
@set fence/display_order = 2
# Get error message
@set fence/get_err_msg = You can't carry around a fence.

# Create the hole
@create/drop hole in fence;hole
#
@lock hole in fence = "get:false();view:attr('look_fence')"
#
@desc hole in fence = Beyond the stacked concrete pylons and mounds of dirt, there's a building with an open doorway. If you squint, you can make out moving shapes in the darkness of the room's interior. Occasionally, a flicker of flame lights the room, and you can see several construction workers conversing with a large creature just out of your sight.
# Set display order
@set hole in fence/display_order = 3
# Get error message
@set hole in fence/get_err_msg = How would that even work?


# Return to the courtyard
courtyard


# Create Locker Room
@dig Lockers = lockers,courtyard;c
#
@desc lockers = This leads to the lockers.
# Set display order
@set lockers/display_order = 2
#
lockers
#
@desc Lockers = You enter the Labyrinth of Lockers, a maze of identical, locked cabinets that can hold all your merchandise! You could start wandering around within the maze, but each path looks identical. Best to stay near the entrance unless you know where you're going.
#
@desc courtyard = Return to the courtyard.

# Create a broken kiosk
@create/drop kiosk : objects.Heavy
# Set the description
@desc kiosk = A note placed over the screen reads: "OUT OF ORDER"


# Return to the courtyard
courtyard

# Create the Chimera gift shop
@dig Gift Shop;ride-room-04 : room_chimera_gift_shop.ChimeraGiftShopRoom = gift shop;gift;shop;g,courtyard;c
#
@desc gift shop = This leads to the gift shop for the Chimera ride!
# Set display order
@set gift shop/display_order  = 4
#
gift shop
#
@desc ride-room-04 =
You are standing in a gift shop with shelves of merchandising lining every wall.
There are stuffed animals, hats, sunglasses, candy, and more!
#
@desc courtyard = Return to courtyard.

# Create the gift shop attendant
@create/drop Sales Clerk : objects.ParkAttendant
#
@desc Sales Clerk = The sales clerk appears eager to sell you items!
#
@set Sales Clerk/talk_to_msg = Sales Clerk: "Welcome to the |rChimera|n gift shop! You can purchase any of these items using your park points!\n             If you don't have enough points, you can ride the |rChimera|n to earn more!\n             Use [|gbuy <item>|n] if you see something you like!"

# Return to the courtyard
courtyard

# Create Ride Entrance
@dig Chimera Ride Entryway = chimera ride entryway;entrance;ride;chimera;entry;r;entryway;chimera ride,courtyard;c
#
@desc chimera ride entryway = This leads to the |rChimera|n ride! It's supposed to be AMAZING!
# Set display order
@set chimera ride entryway/display_order = 1
#
chimera ride entrance
#
@desc Chimera Ride Entryway =
You are in front of the line for the |rChimera|n! A holographic display forms an archway above the start of the queue. It shifts every few minutes, forming different potential scenarios you might experience on the ride. It's amazing that these friendly |rChimeras|n are willing to be ridden by thrill-seeking guests!

If you dare to enter, you should get in line. Looking at the nearby safety information or the wait time display could also be useful.
#
@desc courtyard = Return to the courtyard.

# Sign with safety info
@create/drop safety sign : objects.Heavy
#
@desc safety sign = The sign reads: "Warning! This is an action-packed thrill ride where you will be strapped to the back of a mythical monster! If you suffer from any of these conditions, ride at your own risk: Cryptozoophobia, Acrophobia, Trypophobia, Enochlophobia, Apiphobia, Pyrophobia, Phasmophobia, Chronophobia, Puberty, Self-Consciousness, Motion Sickness, Dizziness... for a full list of all possible concerning conditions, see Guest Services."

# Digital counter for how many people in line
@create/drop line length display : objects.LineLengthSign

# Create the line room
@dig Waiting in Line : room_chimera_line.ChimeraLineRoom = join the line;line;join,leave the line;leave
#
@desc join the line = Go here to step in line for the |rChimera|n ride.
# Set display order
@set join the line/display_order = 1
#
join the line
#
@desc leave the line = If you leave then you will lose your place in the line!
#
@desc Waiting in Line =
You are standing in line for the |rChimera|n ride. Use [|gline length|n] to view your position in line.
There are a few displays on the walls which are meant to keep you entertained
while you wait. A few vendors are hanging around hoping people
will purchase their wares to help pass the time.

# Create the line attendant
@create/drop Line Attendant : objects.ParkAttendant
#
@desc Line Attendant = The line attendant is dressed in a long brown cloak that covers his feet.\nThey also appears to be wearing a fake beard that extends down to knees.\nUse [|gtalk to line attendant|n] to see if they have a way to help pass the time.
#
@set Line Attendant/talk_to_msg = Line Attendant: "Welcome to the |rChimera|n! If you wait then another ride cart will be along shortly.\nIf you are feeling bored, you could talk with others, buy a hot dog, or type [|gplay game|n]"
# Set display order
@set Line Attendant/display_order = 1

# Create the hot dog vendor
@create/drop Hot Dog Vendor;vendor;hot dog;hot;dog : objects.ParkAttendant
#
@desc Hot Dog Vendor = The vendor is selling hot dogs to people in line. Try [|gbuy hot dog|n] to get one for yourself!
#
@set Hot Dog Vendor/talk_to_msg = Hot Dog Vendor: "Howdy there! Would you like to buy a hot dog! It only costs |c2|n points!\n                Type [|gbuy hot dog|n] and I'll have one made up for you in a jiffy!"
# Set display order
@set Hot Dog Vendor/display_order = 2

## Create the boarding zone
#@dig ChimeraBoardingRoom;ride-room-05
## Open a one way door to the boarding zone
#@open boarding zone = ride-room-05
##
#@desc boarding zone = From the front of the line you can step up to the boarding zone to get in a seat.
##
#boarding zone
##
#@desc ride-room-05 =
#You are in a small dark room with nothing but a cart that seats a few people.
#The cart looks fairly well built.

## Create the Ride room
#@dig ChimeraRideRoom = start ride
##
#@desc start ride = This will start the |rChimera|n ride!
##
#start ride
##
#@desc
#The wind is rushing past your ears and lights flash by at amazing speed!
#Suddenly a dragon appears! And a snake!
#Oh my! It appears to be a single animal, more complex than any you've ever seen!
#It's a Chimera!
#The Chimera leaps in front of the cart, causing it to slow down to a stop.
#It then leaps away and the cart rushes up a steep incline.
#A voice over an intercom is saying "Whew! Glad we got away!"
#The Chimera returns and blows fire directly at you!
#Just in time the cart whizzes forward and the Chimera falls down into a trap.
#You have escaped with your life! But what happened to the Chimera? ...
#
## Create the end zone for the chimera ride
#@dig ChimeraRideEndRoom = exit ride
##
#@desc exit ride = The exit for the ride.
##
#exit ride
##
#@desc
#You are in a narrow room that guides people out of the ride.
#There is a giant picture of a Chimera on the wall.
#You are so excited that you want to buy things to help you remember the experience!
#
## Link back to the previously created gift shop
#@open gift shop = ride-room-04
##
#@desc gift shop = The exit to the gift shop for the Chimera ride.
#
## Exit to the gift shop
#gift shop

# If bringing back what is above then uncomment above here
# and remove this leave the line command
leave the line

# Exit back to the courtyard
courtyard
# Exit back to the Entrance
exit park
# Exit back to where the script was executed from
Exit The Ride

# END